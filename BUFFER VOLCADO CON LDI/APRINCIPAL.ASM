






INCLUDE "CONTENIDA.ASM"




	ORG 32768

;;;;;PANTALLA VIRTUAL;;;;;;;;;;;;
PANTVIR	
	DS TAMPAN	
	

INICIO	

	;;;COLOCAMOS LAS VARIABLES PARA EMPEZAR A IMPRIMIR SPRITES

	XOR A
	LD (CSPIM+1),A          ;CONTADOR SPRITES A IMPRIMIR
	
	
	LD HL,SPIMP		;PONEMOS EL INICIO DE SPRITES A
	LD (DIRAMET+2),HL	;IMPRIMIR EN SPIMP

;;;;;;;;LECTURA DE TECLAS;;;;;;;


;;;LEO LOS PUERTOS NECESARIOS Y ACTUALIZO EL REGISTRO L;;;
;;;REGISTRO L: BIT 0=1  DERECHA PULSADA
;;;REGISTRO L: BIT 1=1  IZQUIERDA PULSADA
;;;REGISTRO L: BIT 2=1  DABAJO PULSADA
;;;REGISTRO L: BIT 3=1  ARRIBA PULSADA
;;;REGISTRO L: BIT 4=1  DISPARO PULSADO

NOJOY1	LD L,0

NOJOY23	LD A,$7F         ;PARA EL DISPARO
	IN A,($FE)
TSAL	AND 1
	JP NZ, NOJOY2
	SET 4,L
	

NOJOY2	LD A,$DF         ;TECLA IZQUIERDA
	IN A, ($FE) 
TIZ	AND 2
	JP NZ, MIRATD
JIZ	SET 1,L
	JP MIRATAR

LEEJOY	    ;LEE EL JOYSTICK
	LD A,$FF
	IN A,(31)
	JP FINTE1

MIRATD	
DERE1	LD A, $DF       ;TECLA DERECHA
	IN A, ($FE)
TDER	AND 1 
	JP NZ, MIRATAR
	SET 0,L

MIRATAR		
NOJOY4	LD A, $FB       ;TECLA DE ARRIBA 
	IN A, ($FE)
SALBT	AND 1   
	JP NZ, MIRATAB
	SET 3,L
	JP FINTEC

MIRATAB
QUITO2	LD A, $FD	;TECLA ABAJO
	IN A, ($FE)
TARR89	AND 1     
	JP NZ, FINTEC
	SET 2,L
FINTEC	LD A,L
FINTE1	LD (JOY+1),A	;METO EL RESULTADO EN JOY+1



;;;;;;;;;CONTROL DEL PROTA;;;;;;;;;;

CONTPRO	LD IX, PROTA

TEC15	LD A,(JOY+1)    
	BIT 1,A
	JP Z, NOIZQ

 ;;TECLA IZQUIERDA PULSADA;;

;;SI LA HORIENTACION DEL PROTA ES IZDA, NO CAMBIAMOS EL GRAFICO;;
;;SI ES DERECHA, PONEMOS EL GRAFICO MIRANDO A LA IZQUIERDA;;
;;Y LE DAMOS ESA HORIENTACION

;;LO MISMO CON LA DERECHA

HORIEN	LD A,0		;0=DCHA, 1=IZDA
	OR A
	JP NZ, NOCAMI
	INC A
	LD (HORIEN+1),A
	LD HL,PROTAI	;GRAFICO MIRANDO A LA IZDA
	CALL METEG
	
	
NOCAMI	LD A,(PROTA+1)	;MIRO SI LLEGA AL LIMITE IZQUIERDO
	OR A
	JP Z, NOIZQ
	SUB 2		;RESTAMOS 2 COORDENADAS A LA X
	JP METX1

NOIZQ	
JOY	LD A,0
	BIT 0,A
	JP Z, VERARRIBA

;;TECLA DERECHA PULSADA;;

	LD A,(HORIEN+1)		;0=DCHA, 1=IZDA
	OR A
	JP Z, NOCAMD
	DEC A
	LD (HORIEN+1),A
	LD HL,PROTAD
	CALL METEG
	
	
NOCAMD	LD A,(PROTA+1)
	CP ANPANA-16
	JP Z, VERARRIBA
	ADD A,2
METX1	LD (PROTA+1),A
	CALL GESTFAS	;CAMBIAMOS LA FASE, YA QUE SE HA MOVIDO
	
VERARRIBA
	LD A,(JOY+1)
	BIT 3,A
	JP Z, VERABAJO

;;TECLA ARRIBA PULSADA;;
;;SI NO LLEGA ARRIBA DEL TODO, RESTA 2 A LA COORDENADA Y;;

	LD A,(PROTA)
	OR A
	JP Z, FINTECLAS

	SUB 2
	LD (PROTA),A
	JP FINTECLAS

VERABAJO
	LD A,(JOY+1)
	BIT 2,A
	JP Z, FINTECLAS


;;TECLA ABAJO PULSADA;;
;;SI NO LLEGA ARRIBA DEL TODO, SUMA 2 A LA COORDENADA Y;;
	LD A,(PROTA)
	CP ALPANA-16
	JP Z, FINTECLAS
	
	ADD A,2
	LD (PROTA),A

FINTECLAS

;;;CONTROL DE LOS ENEMIGOS;;;

NUMEN	EQU 6		;EN PRINCIPIO, EL NUMERO MAXIMO DE ENENMIGOS ES 6

CONTE	LD B,NUMEN
	LD IX,ENEM1

;;MOVIMIENTO HORIZONTAL
	
CONTS	LD A,(IX+19)
	BIT 7,A
	JP Z, CONTS1	;MIRAMOS SI VA A MITAD DE VELOCIDAD

	AND 127		;PONEMOS EL BYTE 7 A CERO
	XOR 1
	SET 7,A		;LO PONEMOS A UNO OTRA VEZ
	LD (IX+19),A
	JP Z, PONSP	;CADA DOS VECES, SOLO LO IMPRIME
	
CONTS1	LD A,(IX+1)	;SI LLEGA AL LIMITE IZQUIERDO O DERECHO
	OR A		;CAMBIA LA DIRECCION DEL SPRITE
	JP Z, CHANGE
	LD C,A
	ADD A,(IX+18)
	CP ANPANA
	LD A,C
	JP NC, CHANGE
NOFIS	ADD A,(IX+12)	;SI NO HAY CAMBIO DE SIGNO DEL INCREMENTO,
	LD (IX+1),A	;SIMPLEMENTE ACTUALIZAMOS LA COORDENADA X
	CALL GESTFAS	;CAMBIAMOS LA FASE


;;MOVIMIENTO VERTICAL

	LD A,(IX+0)
	OR A
	JP Z, CHANGEV
	LD C,A
	LD A,(IX+3)
	SLL A
	ADD A,C
	CP ALPANA
	JP NC, CHANGEV

	LD A,C

	ADD A,(IX+11)
	LD (IX+0),A
	

PONSP	CALL METEIMPS	;METEMOS EL SPRITE A LA COLA DE IMPRESION
	LD DE,ENEM2-ENEM1
	ADD IX,DE
	DJNZ CONTS
	

;;;;;;;VER SI DISPARA;;;;;;

PONQDIS	LD A,(JOY+1)   ;DISPARAMOS?
	BIT 4,A
	JP Z, MTT

;;;PONEMOS EL DISPARO

	LD A,$C3	;OPCODE DE JP
	LD (PONQDIS),A
	LD HL,DISPON
	LD (PONQDIS+1),HL

;;;;CON ESTO ANTERIOR PONEMOS EN PONQDIS: JP DISPON	
	
	LD A,20
	LD (DISPON+1),A ;DISPARO ACTIVADO POR 20 MOVIMIENTOS

	LD A,(HORIEN+1)	;MIRO LA HORIENTACION DEL PROTA
	OR A		;SI ES CERO, ES QUE MIRA A LA DERECHA
	JP Z, PONDDCHA

;;PONEMOS EL DISPARO A LA IZDA DEL PROTA;;
	LD C,-8		;POSICION A LA IZDA DEL PROTA
	LD B,-4		;MOVIMIENTO A LA IZDA DEL DISPARO
	JP COLOD
	
PONDDCHA
	LD C,14		;POSICION A LA DCHA DEL PROTA
	LD B,4		;MOVIMIENTO A LA DCHA DEL DISPARO

;;PONEMOS EL DISPARO A LA DCHA DEL PROTA;;

COLOD	LD A,(PROTA)	
	ADD A,6
	LD (DISPARO),A	 ;COLOCO EL DISPARO EN SU POSICION VERTICAL
	LD A,(PROTA+1)
	ADD A,C
	LD (DISPARO+1),A ;COLOCO EL DISPARO EN SU POSICION HORIZONTAL
	LD A,B
	LD (DISPARO+12),A ;COLOCO EL SENTIDO DEL DISPARO


;;;CONTROL DEL DISPARO ACTIVADO;;;;;;;;;

DISPON	LD A,0	
	DEC A
	LD (DISPON+1),A ;DECREMENTAMOS LA DURACION DEL DISPARO
	JP Z, FINDISP	;SI LLEGA A CERO, LO DESACTIVA


	
SIGDIS	LD IX,DISPARO
	LD A,(IX+1)	
	CP 4		;SI EL DISPARO LLEGA A LOS LIMITES DE LA
	JP C, FINDISP	;PANTALLA, SE DESACTIVA
	CP ANPANA-10
	JP NC, FINDISP
	ADD A,(IX+12)	;SI NO, ACTUALIZA LA COORDENADA X
	LD (IX+1),A
	CALL METEIMPS	;MANDAMOS AL DISPARO A LA COLA DE IMPRESION
	
	
MTT	
	;;MANDAMOS AL PROTA A LA COLA DE IMPRESION

	LD IX,PROTA
	CALL METEIMPS
	

;;;;;;;;GUARDAMOS EL FONDO DE TODOS LOS SPRITES A IMPRIMIR;;;;;
	LD IY,DATFON-6		;PUNTERO PARA GUARDAR LOS DATOS DE LOS CACHOS
				;DE PANTALLA DONDE VA A IR EL SPRITE
;;PARA CADA SPRITE SE GUARDAN 6 DATOS
;0=ALTURA/2, 1=ANCHURA, 2,3=DIR BUFFER PANTALLA, 4,5=DONDE SE GUARDA
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	LD (STACKV+1),SP	;GUARDO EL STACK

	LD DE,FONDOS		;A PARTIR DE FONDOS SE GUARDAN LOS FONDOS DONDE
	LD (GFONDI+1),DE	;VA A IR EL SPRITE
	LD SP,SPIMP		;INICIO DE LA COLA DONDE SE METEN LAS TABLAS
				;DE LOS SPRITES A IMPRIMIR
	

CSPIM	LD A,0			;NUMERO DE SPRITES QUE HAY QUE IMPRIMIR
	LD (CSPIMR+1),A		;SE ACTUALIZA EN METEIMPS 
PINTSR	EX AF,AF'
	
	POP IX			;COJO EL DATO DE LA COLA DE IMPRESION DE SPRITES
	
	
	LD DE,6
	ADD IY,DE		;ACTUALIZAMOS IY

	LD L,(IX+0)		;COORD Y
	
	LD H,$F9      		;COGE EL DATO DE LA DIRECCION DEL BUFFER DE LA TABLA
	LD D,(HL)		;DE TIENE LA DIRECCION DE LA LINEA PARA IMPRIMIR
	LD A,(IX+1)		;DIVIDE LA COORDENADA X POR 8
	SRL A 	
	SRL A 
	SRL A 
	DEC H
	ADD A,(HL)		;LO SUMA AL BYTE BAJO DE LA DIRECCION
	LD E,A    		
	JP NC, NOCA
	INC D
	
NOCA	LD (DIRPBUF+1),DE	;DE=DIRECCION DEL BUFFER DONDE VA EL SPRITE
				;SE METE EN LA RUTINA 
				;PARA NO VOLVER A HALLARLO

GF1	EX DE,HL  
	LD (IY+2),L		;LO METEMOS EN SU TABLA
	LD (IY+3),H

GFONDI	LD DE,FONDOS  		;DONDE SE GUARDA EL FONDO DEL DONDE VA EL SPRITE
	LD (IY+4),E		;LO METEMOS EN SU TABLA
	LD (IY+5),D

	LD C,(IX+2)		;ANCHO DEL SPRITE
	LD A,(IX+1)		;SI LA COORDENADA Y ES DIVIDIBLE POR OCHO
	AND 7			;OCUPA LA ANCHURA, SI NO LO ES, HAY QUE INCREMENTARLA
	JP Z, NOINC		;YA QUE OCUPA UN CARACTER MAS
	INC C
NOINC	LD A,C
	LD (IY+1),A		;LO METEMOS EN SU TABLA
	LD (GF6+1),A		;SE METE EN LA RUTINA 
	LD (GF3+1),A		;PARA NO VOLVER A HALLARLO

	NEG			;HALLAMOS LO QUE HAY QUE SUMAR A HL PARA PASAR
	ADD A,ANPAN		;A LA LINEA SIGUIENTE (ANPAN-ANCHO A GUARDAR)
	LD (METAN+1),A
	LD (METAN1+1),A

	LD A,(IX+3)		;ALTURA/2 
	LD (IY+0),A		;LO METEMOS EN SU TABLA

	LD B,0			;REPITE C OSEA QUE B=0	

GF5	
GF6	LD C,0			;ESTE DATO SE METE MAS ARRIBA
GF2	LDI
	JP PE, GF2		;REPITE HASTA QUE BC=0
	
METAN	LD C,0			;SIGUIENTE LINEA
	ADD HL,BC

GF3	LD C,0			;ESTE DATO SE METE MAS ARRIBA
GF4	LDI
	JP PE, GF4		;REPITE HASTA QUE BC=0
	
METAN1	LD C,0			;SIGUIENTE LINEA
	ADD HL,BC		
	
	DEC A			
	JP NZ, GF5	

POSGU	LD (GFONDI+1),DE	;ACTUALIZAMOS GFONDI PARA QUE EL FONDO DEL
				;SIGUIENTE SPRITE LO GUARDE SEGUIDO.
	


;;;IMPRIME EL SPRITE;;;;;;;

IMPBUF	
	
	LD B,(IX+3)	;ALTURA/2
	
DIRPBUF LD DE,0		;ESTE DATO SE METE MAS ARRIBA
	
	   
IMB2	

	LD L,(IX+4)	;DIRECCION DEL GRAFICO
	LD H,(IX+5)

	LD (STACKSP+1),SP		;SE GUARDA EL STACK
	LD SP,HL			;STACK=DIRECCION DEL GRAFICO
	EX DE,HL			;HL=DIRECCION DEL BUFFER

POSAR	LD A,(IX+2)	;ANCHURA DEL SPRITE
	LD (ANF+1),A	;LO METE EN LOS BUCLES PARA QUE SEA MAS RAPIDA LA RUTINA   
	LD (ANF1+1),A	
	
	LD (ANFJ+1),A
	LD (ANFJ1+1),A
	
POSBIT	LD A,(IX+1)    			;SI LA COORDENADA X ES DIVISIBLE POR 8,			
	AND 7				;SIGNIFICA QUE NO HAY ROTACIONES, ASI
	JP Z, IMPBUFCARACTERJUSTO	;QUE LE MANDO A UNA RUTINA ESPECIAL
	ADD A,$F8			;LO SUMO AL PRINCIPIO DE LA TABLA DE 
	LD (INDEX3+1),A			;ROTACIONES Y LO METO EN LA RUTINA
	INC A
	LD (INDEX3V+1),A
	
	
	


;;;LA RUTINA SE REPITE 2 VECES. UNA EN PASADA A DERECHAS Y OTRA A IZQUIERDAS;;

AT33
 	LD DE, ANPAN
ANF	LD C,0			;ANCHO DEL SPRITE
	
LA5	EXX
	POP DE	  		;MASCARA=E GRAFICO =D
INDEX3	LD H,0			;POSICION EN EL BYTE LA METO ARRIBA
	LD L,E
	LD A,(HL)		;MASCARA ROTADA
	CPL			
	EXX		
	AND (HL)		;HACE AND DE LA MASCARA CON EL FONDO
	EXX
	LD L,D			;HAYA LA ROTACION DEL GRAFICO
	OR (HL)			;HACE OR DEL GRAFICO ROTADO CON LO ANTERIOR 
	EXX
	LD (HL),A         	;HASTA AQUI HEMOS IMPRIMIDO EL PRIMER CACHO
	INC HL
	EXX			;HACEMOS LO MISMO CON LA PARTE DEL SIGUIENTE
	INC H			;CARACTER
	LD L,E
	LD A,(HL)
	CPL
	EXX
	AND (HL)
	EXX
	LD L,D
	OR (HL)
	EXX
	LD (HL),A
	DEC C
	JR NZ, LA5		;REPITE LAS VECES QUE OCUPA DE ANCHO EL SPRITE
			
	ADD HL,DE		;SIGUIENTE LINEA
	
	
;;;ESTO ES LO MISMO, PERO YENDO HACIA LA IZQUIERDA;;	
	
ANF1	LD C,0 
LA5V	EXX
	POP DE	   
INDEX3V	LD H,0		;VAMOS A LA IZQUIERDA. AQUI H VALE UNO MAS QUE EN INDEX3	
	LD L,E
	LD A,(HL)
	CPL			
	EXX		
	AND (HL)
	EXX
	LD L,D
	OR (HL)
	EXX
	LD (HL),A          
	DEC HL
	EXX
	DEC H
	LD L,E
	LD A,(HL)
	CPL
	EXX
	AND (HL)
	EXX
	LD L,D
	OR (HL)
	EXX
	LD (HL),A
	DEC C
	JR NZ, LA5V
	
	ADD HL,DE			;SIGUIENTE LINEA

L7	DJNZ AT33			;REPITO (ALTO DEL SPRITE/2) VECES
	
AT10	
STACKSP	LD SP,0				;PONGO EL STACK EN SU SITIO


FINSPR					;LO HAGO CON CUANTOS SPRITES
	EX AF,AF'			;TENGO QUE IMPRIMIR
	DEC A
	JP NZ, PINTSR



;;;VUELCA EL BUFFER EN LA PANTALLA REAL;;;
VOLCADO
	
	
	LD SP,TVOLCADO		;APUNTO EL STACK A LA TABLA DE VOLCADO
	LD BC,TAMPAN		;DONDE VIENE LAS PRIMERAS DIRECCIONES DE 
	LD HL,PANTVIR		;CADA LINEA EN LA PANTALLA REAL
BUCV	POP DE			;DE=PRIMERA DIRECCION DE LA LINEA

REPT 	ANPAN			;HAGO TANTOS LDI COMO ANCHO ES EL BUFFER
	LDI
	ENDM

	
	JP PE, BUCV		;REPITE HASTA QUE BC=0

STACKV	LD SP,0
	
	


;;;;;RESTAURAMOS EL FONDO;;;;;

	;IY ES EL PUNTERO DE LA TABLA PARA RESTAURAR EL FONDO Y APUNTA
	;A LA TABLA DE RESTAURACION DEL ULTIMO SPRITE QUE SE IMPRIMIO
	;LOS FONDOS HAY QUE RESTAURARLOS EN ORDEN INVERSO A COMO SE GUARDARON

CSPIMR	LD A,0		;NUMERO DE SPRITES
GFR10	EX AF,AF'
	LD E,(IY+2)	;DONDE HAY QUE RESTAURAR DEL BUFFER
	LD D,(IY+3)  	
	LD L,(IY+4)	;DONDE ESTA GUARDADO
	LD H,(IY+5)
	LD A,(IY+1)	;ANCHO DEL SPRITE
	LD (GF6R+1),A
	LD (GF3R+1),A

	NEG			;HALLAMOS LO QUE HAY QUE SUMAR A HL PARA PASAR
	ADD A,ANPAN		;A LA LINEA SIGUIENTE (ANPAN-ANCHO) A RECUPERAR
	LD (RECU+1),A
	LD (RECU1+1),A
	
	LD B,0			;REPITE C VECES Y B=0	

	LD A,(IY+0)	;ALTURA/2  DEL SPRITE
GF5R	
GF6R	LD C,0
	
	
GF2R	LDI
	JP PE, GF2R	;REPITE BC VECES (ANCHO GUARDADO)
	
	EX DE,HL
RECU	LD C,0		;SIGUIENTE LINEA
	ADD HL,BC
	EX DE,HL
	
GF3R	LD C,0
	
GF4R	LDI
	JP PE, GF4R
	
	EX DE,HL
RECU1	LD C,0
	ADD HL,BC
	EX DE,HL
	
	DEC A
	JP NZ, GF5R	;LO HACE (ALTURA/2  DEL SPRITE) VECES
	
	LD DE,-6	;SIGUIENTE TABLA DE RESTAURACION
	ADD IY,DE

	EX AF,AF'	;LO HACEMOS CON CUANTOS SPRITES HEMOS IMPRIMIDO
	DEC A
	JP NZ, GFR10		
	
	JP INICIO

IMPBUFCARACTERJUSTO 

;IMPRIME EN CARACTER JUSTO Y NOS AHORRAMOS LA TABLA DE ROTACIONES CERO
;FUNCIONA IGUAL QUE LA ANTERIOR PERO SIN ROTAR NADA.

	

ANFJ	LD C,0	   ;SE METE ARRIBA	  
LA5J	POP DE	   ;MASCARA=E GRAFICO =D
	LD A,E
	CPL			;ESTE VALOR LO METO EN IMPBUF
	AND (HL)
	OR D
	LD (HL),A          ;HASTA AQUI HEMOS IMPRIMIDO EL PRIMER CACHO
	INC HL
	DEC C
	JR NZ, LA5J
	LD DE,ANPAN-1
	ADD HL,DE

ANFJ1	LD C,0	   ;LO METE ARRIBA
LA5JV	POP DE	   ;MASCARA=E GRAFICO =D
	LD A,E
	CPL			;ESTE VALOR LO METO EN IMPBUF
	AND (HL)
	OR D
	LD (HL),A          ;HASTA AQUI HEMOS IMPRIMIDO EL PRIMER CACHO
	DEC HL
	DEC C
	JR NZ, LA5JV
	LD DE,ANPAN+1
	ADD HL,DE
L7J	DJNZ ANFJ
	
	JP STACKSP





	INCLUDE "ARUTINAS.ASM"

	INCLUDE "ASPRITES.ASM"
	INCLUDE "ATABLAS.ASM"

;;;;;TABLA DE COORDENADAS PARA VOLCADO;;;;;

TVOLCADO
	DS 320

;;AQUI GUARDAMOS LOS FONDOS DEL BUFFER QUE SE MACHACAN AL IMPRIMIR LOS SPRITES
	ORG $F500
FONDOS	


;;;;;TABLA DE COORDENADAS PARA IMPRIMIR;;;;;

TABCOOR	ORG $F800
	DS 192
DATFON	DS 100		;DATOS DE LA TABLA DE RECUPERACION DE LOS FONDOS
;0=ALTURA/2, 1=ANCHURA, 2,3=DIR BUFFER PANTALLA, 4,5=DONDE SE GUARDA

TABCOO1	ORG $F900
	DS 192
SPIMP	DS 20		;COLA DE LOS SPRITES QUE SE IMPRIMEN





;;;;TABLA PARA LAS ROTACIONES;;;;
	ORG $FA00