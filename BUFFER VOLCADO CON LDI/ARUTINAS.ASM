	;;QUITAMOS EL DISPARO
FINDISP	LD A,$3A	;OPCODE DE LD A,(NN)
	LD (PONQDIS),A
	LD HL,JOY+1
	LD (PONQDIS+1),HL 
;;CON ESTO ANTERIOR PONEMOS EN PONQDIS: LD A,(JOY+1)
	JP MTT


;;;CAMBIA LA DIRECCION Y EL GRAFICO DE LOS ENEMIGOS

CHANGE	LD A,(IX+12)	;COGEMOS EL INCREMENTO O DECREMENTO EN LA
	NEG		;COORDENADA X Y LO CAMBIAMOS DE SIGNO
	LD (IX+12),A
	JP M, NEGAT	;SI AL NEGAR EL INCREMENTO DE LA COORDENADA X
			;EL NUMERO QUE RESULTA ES NEGATIVO (M) ES QUE SE
			;VA A MOVER A LA IZQUIERDA Y SALTA A NEGAT, CON LO
			;QUE PONE EL GRAFICO MIRANDO A LA IZQUIERDA

POSIT	LD L,(IX+14)	;PONE EL GRAFICO MIRANDO A LA DERECHA
	LD H,(IX+15)
	JP POSNP
NEGAT	LD L,(IX+16)	;PONE EL GRAFICO MIRANDO A LA IZQUIERDA
	LD H,(IX+17)
POSNP	CALL METEG
	
	ADD A,(IX+1)	;ACTUALIZAMOS LA COORDENADA X
	LD (IX+1),A
	JP PONSP

;;CAMBIA DE DIRECCION VERTICAL SI UN ENEMIGO LLEGA A LOS LIMITES DE LA PANTALLA
CHANGEV	LD A,(IX+11)	;COGEMOS EL INCREMENTO O DECREMENTO EN LA
	NEG		;COORDENADA Y Y LO CAMBIAMOS DE SIGNO
	LD (IX+11),A
	ADD A,(IX+0)
	LD (IX+0),A	;ACTUALIZAMOS LA COORDENADA X
	JP PONSP
	





;;RUTINA PARA METER UN SPRITE EN LA COLA DE IMPRESION

METEIMPS
DIRAMET	LD (SPIMP),IX		;METEMOS LA TABLA DEL SPRITE
	LD HL,(DIRAMET+2)	;SUMAMOS DOS AL PUNTERO DE LA COLA
	INC L
	INC L
	LD (DIRAMET+2),HL
	LD HL,CSPIM+1		;INCREMENTAMOS EL CONTADOR DE SPRITES
	INC (HL)		;A IMPRIMIR
	RET

;;METE LA DIRECCION DEL GRAFICO QUE TIENE HL EN LA TABLA DEL SPRITE;;

METEG	LD (IX+6),0		;CONTADOR DE ESPERA DE CAMBIO DE FRAME A CERO
	LD (IX+8),L		;PRIMER FRAME DEL GRAFICO DEL SPRITE
	LD (IX+9),H
	LD (IX+4),L		;FRAME ACTUAL DEL SPRITE
	LD (IX+5),H
	RET


;;CAMBIA EL FRAME DEL SPRITE

GESTFAS	
	LD A,(IX+6)		;CAMBIA DE FRAME CADA 3 VECES QUE SE LE LLAMA
	INC A
	AND 3			;4 AND 3=0
	LD (IX+6),A
	RET NZ
	
FASE	LD A,(IX+7) 		
	CP (IX+10)
	JP Z, FASDER

;NO HEMOS LLEGADO AL ULTIMO FRAME
LA3	
	INC (IX+7)	    ;INCREMENTAMOS EL CONTADOR
	LD L,(IX+4)	    ;CAMBIO DE FASE
	LD H,(IX+5)         ;DE TIENE LA DIRECCION DEL GRAFICO
	LD E,(IX+13)	    ;TAMAÑO SPRITE
	LD D,0
	ADD HL,DE
LA33	LD (IX+4),L	    ;METEMOS EN EL FRAME ACTUAL EL ANTERIOR MAS EL TAMAÑO
	LD (IX+5),H	    ;DEL SPRITE
	RET

;HEMOS LLEGADO AL ULTIMO FRAME
FASDER	LD (IX+7),0		;CONTADOR A CERO
	LD L,(IX+8)		;PONE EL PRIMER FRAME EN EL FRAME ACTUAL
	LD H,(IX+9)		
	LD (IX+4),L
	LD (IX+5),H
	RET 