


ANPAN	EQU 24		;ANCHO DE LA PANTALLA
ANPANA	EQU ANPAN*8	;ANCHO PANTALLA EN ALTA
ALPAN	EQU 16		;ALTO DE LA PANTALLA
ALPANA	EQU ALPAN*8	;ALTO DE LA PANTALLA EN ALTA RESOLUCION
TAMPAN	EQU ALPANA*ANPAN  ;TAMAÑO DEL BUFFER
DIRPAN	EQU $4064	;DIRECCION DE PANTALLA DONDE SE
			;PONE EL CUADRO


	ORG 24000

	DI
	LD SP, $FA00



	;;;;;;;;;;;;ROTACIONES;;;;;;;;;;;;;;;;;;;;;;;;;

;;;EMPIEZAN EN $FA00=64000

TABROT	LD C,3          ;CREA LA TABLA DE ROTACIONES
	LD H,$FA        ;METE BYTES DESPLAZADOS  A LA DERECHA 2,4 Y 6 VECES
LCRTCR	LD A,9		;EN $FA00-$FAFF,$FC00-$FCFF Y $FE00-$FEFF RESPECTIVAMENTE
	SUB C		
	SUB C		;METE EL SOBRANTE DEL DESPLAZAMIENTO EN 
	LD L,0		;EN $FB00-$FBFF,$FD00-$FDFF Y $FF00-$FFFF
ROTACU  LD B,L
	LD E,0
	LD D,A		;SI QUIERO SABER LO QUE VALE $C0 DESPLAZADO 4 VECES A LA
VERROT	DEC D		;COJO EL VALOR DE $FCC0 Y SU SOBRANTE EN $FDC0
	JR Z, VEREXI
	SRL B
	RR E
	JR VERROT
VEREXI	LD (HL),B
	INC H
	LD (HL),E
	DEC  H
	DEC L
	JR NZ, ROTACU
	INC H
	INC H
	DEC C
	JR NZ, LCRTCR

	;;;;;CREA LA TABLA DE COORDENADAS PARA LA RUTINA DE IMPRESION DE SPRITES

CREATABLACOOR		
	
	LD DE,PANTVIR	;INICIO DEL BUFFER
	LD HL,TABCOOR	;INICIO DE LA TABLA $F8YY EL BYTE BAJO Y $F9YY EL ALTO
	LD B,ALPANA	;ALTURA EN PIXELS DEL BUFFER

LINELOOP:	
	LD (HL),E
	INC H
	LD (HL),D
	DEC H
	INC L
	PUSH HL
	LD HL,ANPAN	;ANCHO DEL BUFFER
	ADD HL,DE
	EX DE,HL
	POP HL
	DJNZ LINELOOP
;;SI QUIERO SABER CUAL ES LA DIRECCION DEL BUFFER DE LA COORDENADA Y=26 ($1A)
;;COJO EL VALOR DEL BYTE BAJO DE $F81A Y EL ALTO DE $F91A



	;;;;;CREA LA TABLA DE COORDENADAS PARA LA RUTINA DE VOLCADO
	
	LD IX,TVOLCADO		;PARA VOLCAR MAS RAPIDO EL BUFFER
				;CREO UNA TABLA CON LA DIRECCION
	LD HL,DIRPAN		;DE LA PANTALLA REAL DEL INICIO DE CADA LINEA
	LD B,ALPANA

LINEL1	LD (IX+0),L
	LD (IX+1),H
	CALL SIGLIN
	INC IX
	INC IX
	DJNZ LINEL1

	XOR A
	LD (HORIEN+1),A		;EL PROTA EMPIEZA MIRANDO A LA DERECHA
	LD HL,PROTAD		;GRAFICO DEL PROTA MIRANDO A LA DERECHA
	LD IX,PROTA
	CALL METEG

;;QUITAMOS EL DISPARO
	LD A,$3A	;OPCODE DE LD A,(NN)
	LD (PONQDIS),A
	LD HL,JOY+1
	LD (PONQDIS+1),HL 

;;CON ESTO ANTERIOR PONEMOS EN PONQDIS: LD A,(JOY+1)
	
	XOR A
	OUT ($FE),A
	
	CALL BOREAL		;BORRA LA PANTALLA REAL

	LD HL,PANTVIR		;PINTA LAS RAYAS EN EL BUFFER
	LD DE,PANTVIR+1
	LD (HL),1
	LD BC,TAMPAN-1
	LDIR

	JP INICIO



;;;ESTA RUTINA NOS CALCULA LA DIRECCION DE PANTALLA INMEDIATAMENTE
;;;INFERIOR A UNA DADA EN HL

SIGLIN 	INC H 		;SI DESPUES DE INCREMENTAR H, ESTE NO ES MULTIPLO
      	LD A,H		;DE 8, ENTONCES PERTENECE AL MISMO CARACTER Y SOLO
      	AND 00000111B	;HAY QUE INCREMENTAR H
       	RET NZ

	LD A,L		;SUMAMOS 32 A L
	ADD A,32
      	LD L,A
      	RET C 		;SI ES MAYOR QUE 256, ENTONCES CAMBIA DE TERCIO

       	LD A,H		;SI NO CAMBIA DE TERCIO, TENEMOS QUE RESTAR 8 A H
     	SUB 8
      	LD H,A
      	RET



;;ESPERA A QUE NO SE PULSE UNA TECLA

NPULTEC	XOR A                        ;LEER TODAS LAS TECLAS
  	IN A, ($FE)		     
  	CPL
 	AND $1F                      ;COMPRUEBA TODOS LOS UNOS
  	JR NZ, NPULTEC
  	RET

;;ESPERA A PULSAR UNA TECLA

PULTEC	XOR A                        
  	IN A, ($FE)		     ;LEER TODAS LAS TECLAS
  	CPL
 	AND $1F                      ;COMPRUEBA TODOS LOS UNOS
  	JR Z, PULTEC
  	RET	

;;BORRA LA PANTALLA REAL

BOREAL	

;;BORRA LOS GRAFICOS
	
	LD HL,$4000
	LD D,H
	LD E,L
	INC E
	LD (HL),0
	LD BC,$1800-1
	LDIR

;;BORRA LOS ATRIBUTOS

	LD HL,$5800
	LD D,H
	LD E,L
	INC E
	LD (HL),6	;PONE COLOR AMARILLO
	LD BC,$300-1
	LDIR 
	RET

